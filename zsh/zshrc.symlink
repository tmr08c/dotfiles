# Uncomment the following (and `zrpof` line at the end of the file) to debug
# performance issues
#
# zmodload zsh/zprof

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# load every completion after autocomplete loads ideally, most comleitions will
# actually be coming from zsh-completions in the next step, but this allows us
# to configure zsh's compleition and manually add compleitions if necessary
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# use zsh-copmletions brew package for autocomplete
# https://github.com/zsh-users/zsh-completions
if [[ -a  /usr/local/share/zsh-completions ]]
then
  FPATH=/usr/local/share/zsh-completions:$FPATH

  # initialize autocomplete here, otherwise functions won't be loaded
  autoload -Uz compinit
  # Cache compinit and only refresh one a day
  # See https://htr3n.github.io/2018/07/faster-zsh/ for more
  if [ $(date +'%j') != $(/usr/bin/stat -f '%Sm' -t '%j' ${ZDOTDIR:-$HOME}/.zcompdump) ]; then
    compinit
  else
    compinit -C
  fi
fi
unset config_files

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

source <(fzf --zsh)

# Add direnv hook to load `.envrc` files
if [ -x "$(command -v direnv)" ]; then
  eval "$(direnv hook zsh)"
  # A shortcut to access the asdf-managed version of `direnv`
  direnv() { asdf exec direnv "$@"; }
fi

# zprof
source "${XDG_CONFIG_HOME:-$HOME/.config}/asdf-direnv/zshrc"
